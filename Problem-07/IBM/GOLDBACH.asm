*********************************
*				*
*    Problem 7 - CSL Project    *
*				*
*********************************
* REGISTER USES:
* R2 = N
* R3,R4,R5 = TEMP
* R6 = RETURN ADDRESS
* R7 = NUMBER - A
* R10 = NUMBER
* R11 = A
PROG7		START		0
		STM		14,12,12(13)
		BALR		12,0
		USING		*,12
		SR		1,1		ONLY ONE LINE INPUT
		READCARD	INPUT,EOF
		CONVERTI	2,INPUT
		ST		2,N
		LA		10,4		FIRST EVEN NUMBER
MAINLOOP	LA		11,2		START FROM 2
LOP		ST		11,A
		BAL		6,ISPRIME	CHECK IF A IS PRIME
		BZ		NEXT
		LR		7,10
		SR		7,11		R7 = NUMBER - A
		ST		7,A		A = NUMBER - A
		BAL		6,ISPRIME	CHECK IF NUMBER - A IS PRIME
		BZ		NEXT
		B		PRINT
NEXT		LA		11,1(0,11)	INCREMENT A
		CR		11,10
		BL		LOP
		WTO         	'GOLDBACH CONJECTURE FAILED!!!'
NEXTN		LA		10,2(0,10)	GO TO THE NEXT EVEN NUMBER
		CR		10,2
		BC		12,MAINLOOP	CONTINUE UP TO N (BLE)
* RETURN TO OS
EOF		LM 		14,12,12(13)
		BR		14
* CHECK IF "A" IS PRIME AND UPDATE CC (0: NOT PRIME)
ISPRIME		L		5,A
		C		5,=F'2'
		BE		PRM
		LA		3,2		DIVISOR (R3) = 2
PRLOOP		L		5,A
		XR		4,4
		DR		4,3		REMAINED IN R4
		C		4,=F'0'
		BZ		NOTPRM		IF REMAINDER IS ZERO, IT'S NOT PRIME
		LA		3,1(0,3)	INCREMENT DIVISOR
		C		3,A
		BL		PRLOOP
PRM		CR		10,11		IS PRIME (CC = 10)
		BR		6		RETURN
NOTPRM		CR		3,3		IS NOT PRIME (CC = 00)
		BR		6		RETURN
PRINT		PRINTOUT	10,HEADER=NO
		PRINTOUT	11,HEADER=NO
		PRINTOUT	7,HEADER=NO
		B		NEXTN
* VARIABLES
N		DC		F'0'
A		DC		F'0'
INPUT		DC  		CL80' '
		END 		PROG7